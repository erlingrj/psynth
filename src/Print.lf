target C

preamble {=
  #include "psynth.h"
=}

reactor Print(filename: string = "waves.csv", pg_name: string = "plot.pg", only_last: bool = true) {
  input tsamples: sample_buffer_t*
  input fsamples: fsample_buffer_t*

  state file: FILE* = {= NULL =}
  state cnt: int = 0

  reaction(startup) {=
    self->file = fopen(self->filename, "w");
    if(self->file == NULL) {
      lf_print_error_and_exit("Failed to open file: %s", self->filename);
    } else {
      fprintf(self->file, "Time,Amplitude\n");
    }


    FILE *gnuplotScript = fopen(self->pg_name, "w");
    if (gnuplotScript == NULL) {
        perror("Error creating Gnuplot script file");
        return;
    }

    fprintf(gnuplotScript, "set terminal pdf\n");
    fprintf(gnuplotScript, "set output '%s.pdf'\n", self->filename);
    fprintf(gnuplotScript, "set datafile separator ','\n");
    fprintf(gnuplotScript, "set key autotitle columnhead\n");
    fprintf(gnuplotScript, "plot '%s' with lines\n", self->filename);
    fclose(gnuplotScript);
  =}

  reaction(tsamples) {=
    // If we have the only_last option, then we overwrite each time we have
    // collected enough samples
    if (self->only_last) {
      self->cnt += SAMPLE_SIZE;
      if (self->cnt > 5000) {
        fclose(self->file);
        self->file = fopen(self->filename, "w");
        if(self->file == NULL) {
          lf_print_error_and_exit("Failed to open file: %s", self->filename);
        } else {
          fprintf(self->file, "Time,Amplitude\n");
        }
        self->cnt=0;
      }
    }

    double t = lf_time_logical_elapsed() / 1.0e9;
    for (int i = 0; i<SAMPLE_SIZE; i++) {
      fprintf(self->file,
        "%f,%f\n",
        t,
        tsamples->value->samples[i]
      );
      t += SAMPLE_STEP;
    }
  =}

  reaction(fsamples) {=
    // If we have the only_last option, then we overwrite each time we have
    // collected enough samples
    if (self->only_last) {
      fclose(self->file);
      self->file = fopen(self->filename, "w");
      if(self->file == NULL) {
        lf_print_error_and_exit("Failed to open file: %s", self->filename);
      } else {
        fprintf(self->file, "Time,Amplitude\n");
      }
    }
    // FIXME: Here I only show 1/4 of the spectre because thats where everything is?
    for (int i = 0; i<SAMPLE_SIZE/4; i++) {
      fprintf(self->file,
        "%i,%f\n",
        i,
        // FIXME: Is even the magnitude the right approach here?
        sqrt(fsamples->value->samples[i].r * fsamples->value->samples[i].r + fsamples->value->samples[i].i * fsamples->value->samples[i].i)
      );
    }
  =}

  reaction(shutdown) {=
    fclose(self->file);
    char gnuplotcmd[1024];
    snprintf(gnuplotcmd, sizeof(gnuplotcmd), "gnuplot %s", self->pg_name);
    system(gnuplotcmd);
  =}
}
