target C;


preamble {=
    #include "midi.h"
    #include "freq.h"
    #include "common.h"
    #include <math.h>
    #include <string.h>
=}

reactor AdditiveOscillator(sample_size:int=128) {
  input freq:double
  output samples: sample_buf_t
  sine = new Oscillator(initialType={=OSC_SINE=})
  tri = new Oscillator(initialType={=OSC_TRI=})

  freq-> sine.freq
  freq-> tri.freq

  reaction(sine.samples, tri.samples) -> samples {=
    for (int i = 0; i<SAMPLE_SIZE; i++) {
      samples->value[i] = sine.samples->value[i]*0.5 + tri.samples->value[i]*0.5;
    }
    lf_set_present(samples);
  =}
}

reactor Oscillator(initialType: osc_type_t = {=OSC_SINE=}, sample_size:int=128) {

  preamble {=
    double osc_sine(float *samples, double amplitude, double phase, double freq, double period) {
      for (int i = 0; i<SAMPLE_SIZE; i++) {
        samples[i] = sin(2*PI*phase*freq)*amplitude;
        phase += SAMPLE_STEP;
        if (phase >= period) {
          phase = 0;
        }
      }
      return phase;
    }

    double osc_saw(float *samples, double amplitude, double phase, double freq, double period) {
      for (int i = 0; i<SAMPLE_SIZE; i++) {
        samples[i] = (-1.0 + 2*phase*freq)*amplitude; 
        phase += SAMPLE_STEP;
        if (phase >= period) {
            phase = 0;
        }
      }
      return phase;
    }

    double osc_square(float *samples, double amplitude, double phase, double freq, double period) {
      for (int i = 0; i<SAMPLE_SIZE; i++) {
        if (phase < period/2) {
          samples[i] = -amplitude/2;
        } else {
          samples[i] = amplitude/2;
        }
        phase += SAMPLE_STEP;
        if (phase >= period) {
            phase = 0;
        }
      }
      return phase;
    }

    double osc_tri(float *samples, double amplitude, double phase, double freq, double period) {
      for (int i = 0; i<SAMPLE_SIZE; i++) {
        bool rising = phase < period/2;

        if (rising) {
          samples[i] = (-1.0 + 4*phase*freq) * amplitude; 
          phase += SAMPLE_STEP;
        } else {
          samples[i] = (1.0 - 4*(phase - period/2)*freq)*amplitude; 
          phase += SAMPLE_STEP;
          if (phase >= period) {
            phase = 0;
          }
        }
      }
      return phase;
    }
  =}

  input freq:double
  output samples: sample_buf_t
  input type: osc_type_t

  state phase: double = 0
  state freq: double = 0
  state period: double = 0
  state amplitude: double = 0.5
  state type: osc_type_t = initialType
  timer t(0, 23219 usec) // FIXME: What is the period? It is a function of the sample size.

  reaction(freq) {=
    self->phase = 0;
    self->freq = freq->value;
    self->period = 1/freq->value;
  =}

  reaction(type) {=
    self->type = type->value;
  =}
  
  reaction(t) -> samples {=
    if (self->freq > 0) {
      switch (self->type) {
        case OSC_SINE: 
          self->phase = osc_sine(samples->value, self->amplitude, self->phase, self->freq, self->period);
          break;
        case OSC_SQUARE: 
          self->phase = osc_square(samples->value, self->amplitude, self->phase, self->freq, self->period);
          break;
        case OSC_SAW: 
          self->phase = osc_saw(samples->value, self->amplitude, self->phase, self->freq, self->period);
          break;
        case OSC_TRI: 
          self->phase = osc_tri(samples->value, self->amplitude, self->phase, self->freq, self->period);
          break;
      }
      lf_set_present(samples);
    } 
  =}
}