target C;

import KeyboardInput from "lib/Keyboard.lf"

preamble {=
    #include "midi.h"
    #include <string.h> // memcpy
=}


reactor Sequence {
    output midi_event:char[3]
}

reactor RepeatedSequence(sequence: int[] = {48, 50, 52, 53, 49}, n_sequence: int = 5) extends Sequence {

    timer t_on(0, 1 sec)
    timer t_off(900 msec, 1 sec)

    state idx:int=0

    reaction(t_on) -> midi_event {=
        midi_note_on_t temp = MIDI_NOTE_ON(self->sequence[self->idx]);
        memcpy(midi_event->value, &temp, sizeof(temp));
        lf_set_present(midi_event);
    =}

    reaction(t_off) -> midi_event {=
        midi_note_off_t temp = MIDI_NOTE_OFF(self->sequence[self->idx]);
        memcpy(midi_event->value, &temp, sizeof(temp));
        
        if(++self->idx == self->n_sequence) {
            self->idx=0;
        }

        lf_set_present(midi_event);
    =}
}

reactor RandomSequence(period:time = 2 sec, length:int = 10) extends Sequence {
    timer t(0, period)
    timer tOff(900 msec, period)
    logical action off
    state note_playing:int

    preamble {=
    =}
    reaction(startup) {=
        srand(lf_time_physical());
    =}

    reaction(tOff) -> midi_event {=
        midi_note_off_t temp = MIDI_NOTE_OFF(self->note_playing);
        memcpy(midi_event->value, &temp, sizeof(temp));
        lf_set_present(midi_event);
    =}
    
    reaction(t) -> midi_event, off {=
        self->note_playing = 60 + rand() % 12;
        midi_note_on_t temp = MIDI_NOTE_ON(self->note_playing);
        memcpy(midi_event->value, &temp, sizeof(temp));
        lf_set_present(midi_event);
    =}

}


reactor OscillatorChangeSequence(period:time = 100 msec) extends Sequence {
    timer t(0, period)

    state program:osc_type_t = 0

    reaction(t) -> midi_event {=
        self->program = (self->program+1) % NUM_OSC;
        midi_program_change_t temp = MIDI_PROGRAM_CHANGE(self->program);
        memcpy(midi_event->value, &temp, sizeof(temp));
        lf_set_present(midi_event);
    =}
}